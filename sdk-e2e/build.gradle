apply plugin: 'java-library'
apply plugin: 'kotlin'


buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.8'
    }
}

apply from: 'utils.gradle'

def aerogearAndroidSdkDir = "${project.buildDir}/aerogear-android-sdk"
def androidExampleApk = "$aerogearAndroidSdkDir/example/build/outputs/apk/debug/example-debug.apk"
def serverUrl = "http://${System.env.APPIUM_SERVER ?: "localhost:4723"}/wd/hub"


task checkAppium {
    if (System.env.APPIUM_SERVER == null && !checkSocket(4723)) {
        //checks if local Appium instance is running
        throw new IllegalStateException("Local appium server is not running.")
    }
}

ext {
    aerogearAndroidDir = aerogearAndroidSdkDir
}

apply plugin: 'de.fuerstenau.buildconfig'


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "cz.sazel:akow-lib:0.0.9"
    testCompile "io.appium:java-client:$appium_version"
    testCompile "junit:junit:$junit4_version"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}

test.dependsOn 'cleanTest'
test.dependsOn checkAppium

sourceSets.main.java {
    srcDir new File(buildDir, 'gen')  //includes buildConfig
}

buildConfig {
    packageName = "org.aerogear.sdkE2ETests"
    buildConfigField 'String', 'TEST_APK', androidExampleApk
    buildConfigField 'String', 'APPIUM_SERVER_URL', serverUrl
    buildConfigField 'String', 'ADB_EXECUTABLE', "/usr/bin/adb"
    buildConfigField 'String', 'BROWSER_PACKAGE', 'com.android.chrome'
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

apply from: 'sdk_tasks.gradle'